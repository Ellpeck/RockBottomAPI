import org.gradle.internal.os.OperatingSystem

group 'de.ellpeck.rockbottom'
version '0.2.8'

apply plugin: 'java'
apply plugin: 'idea'

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

project.ext.lwjglVersion = "3.1.6"

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (hasProperty('buildnumber')) {
    version = "${version}-${this.properties['buildnumber']}"
}

repositories {
    mavenCentral()
}

dependencies {
    //LWJGL stuff
    compile "org.lwjgl:lwjgl:$lwjglVersion"
    compile "org.lwjgl:lwjgl-assimp:$lwjglVersion"
    compile "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    compile "org.lwjgl:lwjgl-openal:$lwjglVersion"
    compile "org.lwjgl:lwjgl-opengl:$lwjglVersion"
    compile "org.lwjgl:lwjgl-stb:$lwjglVersion"
    compile "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
    compile "org.lwjgl:lwjgl-assimp:$lwjglVersion:$lwjglNatives"
    compile "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
    compile "org.lwjgl:lwjgl-openal:$lwjglVersion:$lwjglNatives"
    compile "org.lwjgl:lwjgl-opengl:$lwjglVersion:$lwjglNatives"
    compile "org.lwjgl:lwjgl-stb:$lwjglVersion:$lwjglNatives"
    compile "org.joml:joml:1.9.8"

    //Netty
    compile group: 'io.netty', name: 'netty-all', version: '4.1.20.Final'
    //JOPT Simple for launch arg parsing
    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '4.9'
    //Google Commons
    compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
        }
    }
}
